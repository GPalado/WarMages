import java.awt.*
import java.util.regex.Pattern

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'idea'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/jetbrains/spek" }
}

dependencies {
    // Put any libraries here:
    // for example, put this:
    // compile group: 'com.google.code.gson', name: 'gson', version: '1.7.1'
    // to include this library https://github.com/google/gson if you like ...

    // Required dependencies for assignments (not tests):
    compile fileTree(dir: 'lib', include: ['*.jar']) // Include libraries in 'lib' directory

    // Tests are run in main module
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.1.0'
    compile 'com.google.code.gson:gson:2.8.2'
    compile 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.2'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}

// setup the plugin
buildscript {
    ext.kotlin_version = '1.2.30'
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
    repositories {
        mavenCentral()
    }
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

dependencies {
    testCompile 'org.jetbrains.spek:spek-api:1.1.5'
    testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.1.5'
}

jacoco {
    toolVersion = "0.7.6.201602180812"
    reportsDir = file("$projectDir/reports/")
}
jacocoTestReport.dependsOn(test)

findbugs {
    toolVersion = "3.0.1"
    reportsDir = file("$projectDir/findbugsReports")
    effort = "max"
    reportLevel = "high"
}
tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}
gradle.startParameter.excludedTaskNames.add("findbugsTest")

task(handleFindbugsFailure) {
    onlyIf {
        findbugsMain.state.failure != null
    }
    doFirst {
        print 'There were probably bugs found by Findbugs! If you see this error on the build ' +
                'server, then build the project locally to see the error info.'

        if (Desktop.isDesktopSupported()) {
            // Open findbugs report in system browser
            Desktop.desktop.browse new File("findbugsReports/main.html").toURI()
        }
    }
}
findbugsMain.finalizedBy handleFindbugsFailure

version '1.0-SNAPSHOT'

sourceSets {
    test {
        java.srcDir 'src/main/java'
    }
}

run {
    standardInput = System.in
    enableAssertions = true
//    args = ["--debug"]
}

task(lint) {
    def lintOut = new ByteArrayOutputStream()
    doFirst {
        javaexec {
            main = "-jar"
            args = [
                    "codecheck/checkstyle-8.1-all.jar",
                    "-c",
                    "codecheck/google_checks.xml",
                    "src/",
            ]
            standardOutput = lintOut
        }
    }
    doLast {
        // Check for any [WARN] or any [STUFF] lines and fail if they exist

        String output = lintOut.toString()
        def errorPattern = Pattern.compile "^\\[\\w+].*", Pattern.MULTILINE
        // Hack to check if multiline regex matches any line
        if (output.replaceAll(errorPattern, "") != output) {
            throw new Error(output)
        } else {
            // Just print output if no detected errors
            println(output)
        }
    }
}

check.dependsOn lint

mainClassName = 'main.Main'

task(buildAndContinueWhenErrors) {
}
buildAndContinueWhenErrors.dependsOn(assemble)
buildAndContinueWhenErrors.finalizedBy(lint)
buildAndContinueWhenErrors.finalizedBy(test)

// Submit task
def withDependenciesTag = '-with-dependencies'
def exportPath = './submit/'
def exportedJarFileName = 'app.jar'
task submit << {

    delete exportPath

    println "Exporting Java files and resources for assignment submission: $exportPath"

    // Copy jar
    copy {
        from './build/libs/'
        include "$project.name$withDependenciesTag*.jar"
        into exportPath
        rename { fileName ->
            if (fileName.endsWith(".jar")) {
                return exportedJarFileName
            }
            return fileName
        }
    }

    copy {
        from './resources/'
        into exportPath + 'resources'
    }
}
submit.dependsOn assemble

// Export jar that includes the dependencies
// (copied from http://www.mkyong.com/gradle/gradle-create-a-jar-file-with-dependencies/)
task fatJar(type: Jar) {
    manifest {
        attributes(
                'Implementation-Title': 'Gradle Project',
                'Implementation-Version': version,
                'Main-Class': mainClassName,
        )
    }
    baseName = project.name + withDependenciesTag
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
}
jar.dependsOn fatJar

jar {
    // Copy Java files into jar
    from sourceSets.main.allSource
}

clean {
    delete exportPath
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
